buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:2.0.1'
    }
}

plugins {
    id "org.sonarqube" version "2.7"
    id "maven-publish"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'org.owasp.dependencycheck'

apply from: 'gradle/sonar.gradle'
sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar.baseName = projectName

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName = projectName
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

javadoc {
    options.charSet = 'UTF-8'
}

jacoco {
    toolVersion = '0.8.0'
}

wrapper {
    gradleVersion = "6.0.1"
}

sonarqube {
    properties {
        property 'sonar.projectName', projectName
        property 'sonar.cpd.exclusions', '**/src/test/**'
        property 'sonar.coverage.exclusions', '**/config/**,**/src/test/**'
        property 'sonar.exclusions', '**/Application.java'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://artifactory.intranet.opt/artifactory/libs-snapshot/' }
    maven { url 'https://artifactory.intranet.opt/artifactory/libs-release/' }
}

configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:19.0'
    }
}

dependencies {
    compile 'net.logstash.logback:logstash-logback-encoder:5.3'
    compile 'org.springframework.boot:spring-boot-starter:2.0.8.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-test:2.0.8.RELEASE'

}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/opt-nc-dev/opt-logging")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

task createPom {
    pom {
        project {
            artifactId projectName
            inceptionYear '2017'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://gitlab.intranet.opt/opt-core/opt-logging.git'
                connection 'scm:git@gitlab.intranet.opt:opt-core/opt-logging.git'
                developerConnection 'scm:git@gitlab.intranet.opt:opt-core/opt-logging.git'
            }
        }
    }.writeTo("build/libs/${projectName}-${version}.pom")
}
